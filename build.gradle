/**
 * 1) Run './gradlew dumpSources
 * 2) Check changeset into source control
 */
def agpVersion = project.file('version.txt').text.trim()

repositories {
  google()
  jcenter()
}

configurations {
  agp
}

dependencies {
  agp "com.android.tools.build:gradle:${agpVersion}"
}

task dumpSources {
  def outputDir = file("sources/")
  inputs.files configurations.agp
  outputs.dir outputDir

  doFirst {
    outputDir.delete()
  }

  doLast {
    def componentIds = configurations.agp.incoming.resolutionResult.allDependencies
        .findAll { it.selected.id.group.startsWith('com.android.tools') }
        .collect { it.selected.id }
        .toSet()
    ArtifactResolutionResult result = dependencies.createArtifactResolutionQuery()
        .forComponents(componentIds)
        .withArtifacts(JvmLibrary, SourcesArtifact)
        .execute()
    result.resolvedComponents.each { ComponentArtifactsResult component ->
      Set<ArtifactResult> sources = component.getArtifacts(SourcesArtifact)
      sources.each { ArtifactResult ar ->
        println "Found ${ar.file}."
        if (ar instanceof ResolvedArtifactResult) {
          def group = ar.id.componentIdentifier.group
          def module = ar.id.componentIdentifier.module
          def version = ar.id.componentIdentifier.version
          println "Extracting to $outputDir/$group/$module."
          copy {
            from zipTree(ar.file)
            into new File(outputDir, "$group/$module")
          }
          println "Done extracting $module."
        }
      }
    }
  }
}
